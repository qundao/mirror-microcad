// Copyright © 2024-2025 The µcad authors <info@ucad.xyz>
// SPDX-License-Identifier: AGPL-3.0-or-later

/// Circle definition
///
/// Examples:
/// * With radius `r`: `Circle(r = 10.0mm);`
/// * With diameter `d`: `Circle(d = 5.0mm);`
pub sketch Circle(radius: Length, center = (x = 0.0mm, y = 0.0mm)) {
    init(r: Length, c = (x = 0.0mm, y = 0.0mm)) {
        radius = r;
        center = c;
    }

    init(diameter: Length, center = (x = 0.0mm, y = 0.0mm)) {
        radius = diameter / 2.0;
    }

    init(d: Length, c = (x = 0.0mm, y = 0.0mm)) {
        radius = d / 2.0;
        center = c;
    }

    __builtin::geo2d::Circle(radius = radius / 1mm, cx = center.x / 1mm, cy = center.y / 1mm);
}

/// A frame with a thickness.
pub sketch Frame(width: Length, height: Length, thickness: Length) {
    std::debug::assert(thickness > 0.0mm, "Thickness must be a positive number (thickness = {thickness})");
    Rect(width, height) - Rect(width = width - 2*thickness, height = height - 2*thickness)
}

/// A regular convex polygon with `n` corners.
pub sketch Ngon(n: Integer, radius: Length) {
    std::debug::assert(n > 2, "N must be at least 3.");
    __builtin::geo2d::Ngon(n, radius = radius / 1mm);
}

/// A regular hexagon.
pub sketch Hexagon(radius: Length) {
    Ngon(6, radius);
}

/// Pie definition.
pub sketch Pie(radius: Length, start_angle: Angle, end_angle: Angle) {
    __builtin::geo2d::Pie(radius = radius / 1mm, start_angle, end_angle);
}

/// Rectangle definition.
///
/// Examples:
/// * Centered rect with width `w` and height `h`: `Rect(width = 10.0mm, height = 5.0mm);`
pub sketch Rect(width: Length, height: Length, x: Length, y: Length) {
    init(width: Length, height: Length) {
        x = -width / 2.0; // center x
        y = -height / 2.0; // center y
    }

    init(w: Length, h: Length) {
        width = w;
        height = h;
        x = -w / 2.0; // center x
        y = -h / 2.0; // center y
    }

    init(size: Length) {
        width = size;
        height = size;
        x = -size / 2.0; // center x
        y = -size / 2.0; // center y
    }

    __builtin::geo2d::Rect(
            width = width / 1mm,
            height = height / 1mm,
            x = x / 1mm,
            y = y / 1mm
    );
}


pub sketch Line(p0: Vec2, p1: Vec2) {
    init(width: Length, y = 0.0mm) {
        p0 = (x = -width / 2mm, y = y / 1mm);
        p1 = (x =  width / 2mm , y = y / 1mm);
    }

    init(height: Length, x = 0.0mm) {
        p0 = (x = x / 1mm, y = -height / 2mm);
        p1 = (x = x / 1mm, y =  height / 2mm);
    }

    init(width: Length, p: (x: Length, y: Length)) {
        p0 = (x = p.x / 1mm, y = p.y / 1mm);
        p1 = (x = (p.x + width) / 1mm , y = p.y / 1mm);
    }

    __builtin::geo2d::Line(x0 = p0.x, y0 = p0.y, x1 = p1.x, y1 = p1.y);
}

pub sketch Ring(outer_r: Length, inner_r: Length ) {
     init(outer_r: Length, thickness: Length) {
         inner_r = outer_r - thickness;
     }

     init(outer_d: Length, inner_d: Length ) {
         outer_r = outer_d/2;
         inner_r = inner_d/2;
     }
     init(outer_d: Length, thickness: Length) {
         inner_r = outer_d/2 - thickness;
         outer_r = outer_d/2;
     }

     Circle(radius = outer_r) - Circle(radius = inner_r) 
}


/// Text definition.
pub sketch Text(height: Length, text: String, font_file = "") {
    __builtin::geo2d::Text(height = height / 1mm, text, font_file);
}
