# Quantity

*Quantities* are numeric values coupled with a unit.
Each unit refers to example one quantity type.

The following quantity types are supported:

| Type      | Metric Units                                | Imperial Units                 |
| --------- | ------------------------------------------- | ------------------------------ |
| `Scalar`  | -, `%`                                      | -                              |
| `Length`  | `µm`, `mm`, `cm`, `m`                       | `in` or `"`, `ft` or `'`, `yd` |
| `Angle`   | `°` or `deg`, `grad`, `turn`,`rad`          |                                |
| `Area`    | `µm²`,`mm²`,`cm²`,`m³`                      | `in²`, `ft²` , `yd²`           |
| `Volume`  | `µm³`, `mm³`,`cm³`,`m³`,`ml`,`cl`,`l`, `µl` | `in³`, `ft³` , `yd³`           |
| `Density` | `g/mm³`                                     | -                              |
| `Weight`  | `g`, `kg`                                   | `lb`, `oz`                     |

**Note**: More units [may be implemented](https://github.com/Rustfahrtagentur/microcad/issues/76).

## Literals

Quantities can be declared by *literals*.
This means that your will automatically get the following type if you use the beside units:

[![test](.test/quantity_types_number_literals.svg)](.test/quantity_types_number_literals.log)

```µcad,quantity_types_number_literals
// declare variable `height` of type `Length` to 1.4 Meters
height = 1.4m;

// use as *default* value in parameter list
fn f( height = 1m ) {}

// calculate a `Length` called `width` by multiplying the
// `height` with `Scalar` `2` and add ten centimeters
width = height * 2 + 10cm;
```

## Examples

### Scalar

The type `Scalar` contains a floating number (without any *unit*) and must be written with at least one decimal place (or in percent).

[![test](.test/types_quantity_scalar.svg)](.test/types_quantity_scalar.log)

```µcad,types_quantity_scalar
zero = 0;
pi =  3.1415;
percent = 55%;
```

### Length

`Length` is used to describe a concrete length.

[![test](.test/types_quantity_length.svg)](.test/types_quantity_length.log)

```µcad,types_quantity_length
millimeters = 1000mm;
centimeters = 100cm;
meters = 1m;
inches = 39.37007874015748in;

std::debug::assert( [millimeters, centimeters, meters, inches].all_equal() );
```

### Angle

Angles are used with rotations and in constrains when proving measures.

[![test](.test/types_quantity_angle.svg)](.test/types_quantity_angle.log)

```µcad,types_quantity_angle
pi = std::math::PI;
radian = 1rad * pi;
degree = 180°;
degree_ = 180deg;
grad = 200grad;
turn = 0.5turn;

std::debug::assert( [degree, degree_, grad, turn, radian].all_equal() );
```

### Area

An `Area` is a two-dimensional quantity. It is the result when multiplying two `Length`s.

[![test](.test/types_quantity_area.svg)](.test/types_quantity_area.log)

```µcad,types_quantity_area
a = 3cm;
b = 2cm;
area = a * b;
std::debug::assert(area == 6cm²);
```

Here is an example of how to use different areal units:

[![test](.test/types_quantity_area_units.svg)](.test/types_quantity_area_units.log)

```µcad,types_quantity_area_units
square_millimeter = 100000mm²;
square_centimeter = 1000cm²;
square_meter = 0.1m²;
square_inch = 155in²;

std::debug::assert(square_millimeter == 0.1m²);
std::debug::assert(square_centimeter == 0.1m²);
```

### Volume

A `Volume` is a three-dimensional quantity. It is the result when multiplying three `Length`s.

[![test](.test/types_quantity_volume.svg)](.test/types_quantity_volume.log)

```µcad,types_quantity_volume
a = 3mm;
b = 2mm;
c = 4mm;

volume = a * b * c;

std::debug::assert(volume == 24mm³);
```

Here is an example for units:

[![test](.test/types_quantity_volume_units.svg)](.test/types_quantity_volume_units.log)

```µcad,types_quantity_volume_units
cubic_millimeter = 1000000.0mm³;
cubic_centimeter = 100.0cl;
cubic_meter = 0.001m³;
cubic_inch = 61.0237in³;
liter = 1.0l;
centiliter = 100.0cl;
milliliter = 1000.0ml;

std::debug::assert(cubic_millimeter == 1.0l);
std::debug::assert(cubic_centimeter == 1.0l);
std::debug::assert(cubic_meter == 1.0l);
std::debug::assert(centiliter == 1.0l);
std::debug::assert(milliliter == 1.0l);
```

### Density

TODO

### Weight

Weights can be calculated by applying volumes to materials.

[![test](.test/types_quantity_weight.svg)](.test/types_quantity_weight.log)

```µcad,types_quantity_weight
gram = 1000.0g;
kilogram = 1.0kg;
pound = 2.204623lb;

std::debug::assert([gram, kilogram].all_equal());
```

## Arithmetic

Quantity types can use operators:

[![test](.test/types_quantity_arithmetic.svg)](.test/types_quantity_arithmetic.log)

```µcad,types_quantity_arithmetic
use std::debug::assert;

a = 2.0mm;
assert([a, 2mm, 2.0mm].all_equal());
b = 2 * a;
assert([b, 4mm, 4.0mm].all_equal());
c = a / 2;
assert([c, 1mm, 1.0mm].all_equal());
d = a + 2.0mm;
assert([d, 4mm, 4.0mm].all_equal());
e = a - 2.0mm;
assert([e, 0mm, 0.0mm].all_equal());
```
