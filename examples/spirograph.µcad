use std::geo2d::*;
use std::ops::*;

const TEETH_SIZE = 0.5mm;
const THICKNESS = 2mm;

sketch GearProfile(teeth: Integer) {
    prop module = TEETH_SIZE;
    prop radius = teeth * module;
    SinusoidalGearProfile(module, teeth, stretch = 0.6, roundness = 0.6);
}


sketch RingProfile(outer_teeth: Integer, inner_teeth: Integer, border_width = 7mm) {
    inner_gear = GearProfile(inner_teeth);
    outer_gear = GearProfile(outer_teeth);
    prop radius = outer_gear.radius;
    prop shift = inner_gear.radius * 40%;

    sector = {
        disc = Circle(outer_gear.radius - border_width);
        sector = Sector(radius = outer_gear.radius, start = 30°, end = 90°).rotate(60°);
        inner_disc = Circle(inner_gear.radius - border_width * 250%);
        ring = Ring(outer_r = inner_gear.radius + border_width - TEETH_SIZE * 2, thickness = border_width);

        (disc - (sector - inner_disc) - ring.translate(x = shift))
            .buffer(-TEETH_SIZE * 2)
            .buffer(TEETH_SIZE * 2);
    };

    outer_gear - sector - inner_gear.translate(x = shift);
}

part PencilHole(pencil_hole_diameter = 0.5mm, offset = 0.0mm) {
    Circle(d = pencil_hole_diameter)
        .extrude(THICKNESS, scale = (x = 300%, y = 300%))
        .translate(y = offset);
}

outer_gear = GearProfile(180);
border_width = 7mm;

frame = {
    border = RoundedRect(outer_gear.radius * 200% + border_width, radius = border_width * 200%);    
    holes = {
        w = 7mm;
        c = Circle(w);
        (c | c.translate(x = w))
            .hull()
            .translate(x = border.width * 56%)
            .rotate([0..3] * 90° + 45°);
    };
    border - outer_gear - holes;
};





ring_1 = RingProfile(outer_teeth = 180, inner_teeth = 120);
ring_2 = RingProfile(outer_teeth = 120, inner_teeth = 72);
ring_3 = RingProfile(outer_teeth = 72, inner_teeth = 36);

 #[color = std::color::BLUE]
frame.extrude(THICKNESS);


#[color = std::color::GREEN]
ring_1.extrude(THICKNESS);

#[color = std::color::RED]
{
    ring = ring_2.extrude(THICKNESS);
    pencil_holes = {
        PencilHole(offset = [4..7] * ring_2.radius / 8);
        PencilHole(offset = ([4..7] + 0.5) * ring_2.radius / 8).rotate(-22.5°);
        PencilHole(offset = ([4..7] + 1/3) * ring_2.radius / 8).rotate(22.5°);
    }.rotate(90°);
    ring - pencil_holes;
}.translate(x = ring_1.shift);


#[color = std::color::YELLOW]
{
    ring = ring_3.extrude(THICKNESS);
    pencil_holes = {
        PencilHole(offset = [4..7] * ring_3.radius / 8);
        PencilHole(offset = ([4..7] + 0.5) * ring_3.radius / 8).rotate(-22.5°);
        PencilHole(offset = ([4..7] + 1/3) * ring_3.radius / 8).rotate(22.5°);
    }.rotate(90°);
    ring - pencil_holes;
}.translate(x = ring_2.shift + ring_1.shift);

gear = {
    profile = GearProfile(36);
    gear = profile.extrude(THICKNESS);
    pencil_holes = {
        PencilHole(offset = [1..3] * profile.radius / 4);
        PencilHole(offset = ([1..3] + 0.5) * profile.radius / 4).rotate(120°);
        PencilHole(offset = ([1..3] + 1/3) * profile.radius / 4).rotate(240°);
    }.rotate(90°);

    gear - pencil_holes;
};

#[color = std::color::TEAL]
gear.translate(x = ring_1.shift + ring_2.shift + ring_3.shift);
