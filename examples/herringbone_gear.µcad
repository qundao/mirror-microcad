use std::ops::*;
use std::geo2d::*;
use std::math::*;

part Gear(teeth: Integer) {
    gear_profile = InvoluteGearProfile(1.5mm, teeth);
    prop outer_radius =  gear_profile.outer_radius;
    prop pitch_radius =  gear_profile.pitch_radius;
    prop tooth_angle =  gear_profile.tooth_angle;

    plate = Circle(gear_profile.pitch_radius - gear_profile.module * 4);

    {
        holes = (
            Pie(plate.radius, 360° / 5, offset = [1..5] * 360° / 5 * 99%)  - Circle(gear_profile.module * 10.0))
                .buffer(-plate.radius * 15%)
                .buffer(plate.radius * 12%);

        profile = {
            if teeth <= 30 {
                Hexagon(gear_profile.module * 4.0);
            } else {
                InvoluteGearProfile(3.0mm, 7);
            }
        };

        plate - holes - profile;

    }.extrude(gear_profile.tooth_distance * 300%).align();

    (gear_profile - plate)
        .extrude(gear_profile.tooth_distance * 2.0, gear_profile.tooth_angle)
        .mirror(-Z)
}

gear1 = Gear(90);
gear2 = Gear(30);

gear1.reflect(X);
gear2.rotate(gear2.tooth_angle * 0.5).translate(x = gear1.outer_radius + gear2.pitch_radius).rotate([0..3] * 360°/3);

