use std::geo2d::*;
use std::ops::*;
use std::math::*;

const SPACING = 8mm;
const THICKNESS = 1.2mm;


part LegoBrick(rows: Integer, columns: Integer, base_height = 9.6mm) {
    prop width = columns * SPACING - 0.2mm;
    prop height = rows * SPACING - 0.2mm;

    base = {
        r = rows - 1;
        c = columns - 1;
        Frame(width, height, THICKNESS);
        Ring(outer_d = 6.51mm, inner_d = 4.8mm)
            .repeat(r*c)
            .distribute_grid(width = width - width / columns, height - height / rows, rows = r, columns = c);
    }
    .extrude(base_height - THICKNESS);

    knobs = Circle(d = 4.8mm)
        .repeat(rows * columns)
        .distribute_grid(width, height, rows, columns)
        .extrude(1.7mm);

    cap = Rect(width, height).extrude(THICKNESS);

    { base; cap; knobs; }.align(Z).union();
}

{
    LegoBrick(rows = 4, columns = 2);
    LegoBrick(rows = 3, columns = 2);
}.distribute_grid(100mm, rows = 2, columns = 2);
